Editor = require "./editor"
COMRichText = require "./com/richText"
Decoration = require "./com/decoration"
COMSpell = require "./com/spell"
COMElement = require "./com/element"
COMNamespace = require "./com/namespace"
COMNode = require "./com/node"
COMContext = require "./com/context"
COMComposer = require "./com/composer"
COMMergeStrategy = require "./com/mergeStrategy"

exports.register = (pack)->
    if pack.decMaintainers?.length > 0 or pack.spells?.length > 0
        COMRichText.packs.push pack
    pack.applyTo COMContext.namespace
    Editor.packs.push pack

class Pack
    constructor:(packs...)->
        @decMaintainers = []
        @spells = []
        @elements = []
        @composers = []
        @merges = []
        @add packs...
        @cmds = []
        @hotkeys = []
    add:(Cons...)->
        for item in Cons
            if item.prototype instanceof Decoration.DecorationMaintainer
                @decMaintainers.push item
            else if item.prototype instanceof COMSpell
                @spells.push item
            else if item.prototype instanceof COMNode
                @elements.push item

            # Note: register composer use INSTANCE of composer
            # The following 2 statement are for this
            else if item instanceof COMComposer
                @composers.push item
            else if item.prototype instanceof COMComposer
                @composers.push new item()
            else if item instanceof COMMergeStrategy
                @merges.push item
            else if item.prototype instanceof COMMergeStrategy
                @merges.push new item()
            else
                console.error "unknown inline resource",item
    registerCommand:(cmd)->
        @cmds.push cmd
    registerHotkey:(keyString,handler)->
        @hotkeys.push {keyString,handler}
    
    applyTo:(target)->
        if target.decorationMaintainers
            dms = (@decMaintainers.map (Ctor)-> new Ctor)
            target.decorationMaintainers.push dms...
        if target.availableSpells
            target.availableSpells.push @spells...
        if target instanceof COMNamespace
            for item in @elements
                target.register item
            for c in @composers
                target.registerComposer c.type,c
            for m in @merges
                target.registerMergeStrategy m
exports.Pack = Pack
